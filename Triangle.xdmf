// Gmsh project created on Tue Sep 22 08:49:23 2020
// Make a rounded triangle
SetFactory("OpenCASCADE");

// Parameters
r1a = 2;        // Characteristic dimensions
r1b = 5;
mesh = 1; //mesh size inside the bow ties 

Point(1) = {0, 0, 0, mesh};
Point(2) = {r1a*Cos(Pi/6), r1a*Sin(Pi/6), 0, mesh};
Point(3) =  {-r1a*Cos(Pi/6), r1a*Sin(Pi/6), 0, mesh};
Point(4) = {0, -r1a, 0, mesh};

// Two arcs of the circle
Circle(1) = {2, 1, 3};      // Top arc
Circle(2) = {3, 1, 4};     // LH bottom arc
Circle(3) = {4, 1, 2};     // LH bottom arc

// Now move them - this makes new Points, 5-10
Translate {0, r1b, 0} { Curve{1}; }   
Translate {-r1b*Cos(Pi/6), -r1b*Sin(Pi/6), 0} { Curve{2}; }  
Translate {+r1b*Cos(Pi/6), -r1b*Sin(Pi/6), 0} { Curve{3}; }  

// Connect the sides of the triangle 
Line(4) = {10, 5};
Line(5) = {6, 7}; 
Line(6) = {8, 9};

// Curve Loop replaced Line Loop in GMSH 4.0
Curve Loop(10) = {1, 5, 2, 6, 3, 4};    // Domain border 

Plane Surface(1) = {10};   // Make the surface, this can translated, rotated

// Reflect about the Y axis 
// Symmetry {0,5,0,0}{Duplicata{Surface{1};}}

// Move the reflection; Change Translate {0,Y,0} for spacing between objects 
Translate {0,16,0} {Symmetry {0,1,0,0}{Duplicata{Surface{1};}}}

// Create Border 
//Domain: Width - 33.4 Height - 45

mesh2 = 1.5; //Mesh size outside of the bow ties 

Point(17) = {-16.74, -14.5, 0, mesh2}; // Bottom left corner 
Point(18) = {16.74, -14.5, 0, mesh2}; // Bottom right corner 
Point(19) = {16.74, 30.5, 0, mesh2}; // Top right corner 
Point(20) = {-16.74, 30.5, 0, mesh2}; // Top left corner 

//Make border lines 
Line(13) = {17,18}; // Bottom line 
Line(14) = {18, 19}; // Right line 
Line(15) = {19, 20}; // Top line 
Line(16) = {17,20}; // Left Line 

Curve Loop(12) = {15, -16, 13, 14};
Curve Loop(13) = {10, 11, 12, 7, 8, 9};
Curve Loop(14) = {5, 2, 6, 3, 4, 1};

Plane Surface(3) = {12, 13, 14}; //Create plane surface 

//Make fine mesh between bow ties 

mesh3 = .08; //Mesh around the center line 

Point(22) = {-1.5, 8, 0, mesh3};
Point(24) = {1.5, 8, 0, mesh3};

Line(17) = {22, 24};

Line{17} In Surface{3}; //Makes the line a part of the surface 

//Change color of mesh 
Color DarkGrey{Surface{3};}
Color Blue{Surface{1};}
Color Yellow{Surface{2};}

//Hide the line in the center 
Hide {Line(17)}


